plugins {
    id 'java'
    id 'maven-publish'
    id("xyz.jpenilla.run-paper") version "3.0.2"
    id("io.papermc.paperweight.userdev") version "2.0.0-beta.19"
    id("com.gradleup.shadow") version "9.2.2"
    id("com.github.node-gradle.node") version "7.1.0"
}

group = 'dev.misieur'
version = '1.0-SNAPSHOT'

repositories {
    mavenCentral()
    maven {
        name = "papermc-repo"
        url = "https://repo.papermc.io/repository/maven-public/"
    }
    maven {
        name = "sonatype"
        url = "https://oss.sonatype.org/content/groups/public/"
    }
    maven {
        url = uri("https://repo.opencollab.dev/main/")
    }
    mavenLocal()
}

dependencies {
    paperweight.paperDevBundle("1.21.11-R0.1-SNAPSHOT")
    compileOnly 'org.projectlombok:lombok:1.18.32'
    annotationProcessor 'org.projectlombok:lombok:1.18.32'

    implementation(project(":furnace"))

    compileOnly('org.geysermc.floodgate:api:2.2.4-SNAPSHOT')
}

tasks {
    runServer {
        minecraftVersion("1.21.11")
    }
}

def targetJavaVersion = 21
java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'

    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        options.release.set(targetJavaVersion)
    }
}

node {
    download = true
    version = "22.18.0"
}

tasks.register('buildNpm', NpmTask) {
    dependsOn installNpm
    workingDir = file('webeditor')
    args = ['run', 'build']
}

tasks.register('installNpm', NpmTask) {
    workingDir = file('webeditor')
    args = ['install']
}

tasks.processResources {
    dependsOn buildNpm
    from('webeditor/dist') {
        include '*.html'
        into 'generated'
    }
}

processResources {
    def props = [version: version]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching('plugin.yml') {
        expand props
    }
}

tasks.shadowJar {
    archiveClassifier.set("")
    minimize()
    from("LICENSE.MD") {
        into("META-INF")
    }
}